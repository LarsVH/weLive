import ~.peerElection;
import ~.misc;
import /.at.lang.futures exclude seconds,minutes,millisec;
import /.at.support.timer;

object:{
	def weLiveRemoteInterface := object: {
		
		def parent;
		def init(prnt) {
			parent := prnt;
		};
		def getID(){
			parent.ID;
		};		
		def getStartPlayBoard(){
			parent.startBoard;
		};
		def getSessionID(){
			parent.currSession.ID;
		};
		def getSession(){
			parent.currSession;
		};
		def getStatus(){
			parent.status;
		};
		def getLeader(){
			parent.leader;
		};
		def requestJoin(joinID, session){
				def joiner := parent.onlinePlayers.get(joinID);
				if: (parent.sessionPlayers.contains(joinID)) then: {
					system.println("WARNING -- joiner (" + joinID + ") already in-game");
					// our joiner is already in the game
					// this case can happen when init code is not finished when game already started
				}
				else: {
				system.println("INFO -- new joiner (" + joinID + "), adding him to joinPlayers");
				parent.joinPlayers.put(joinID, joiner);
				};
		};
		def requestLeaderShip(remoteID){
			if: (remoteID != parent.leader) then: {					
				if: (IDsmaller(remoteID, parent.ID)) then: {
					system.println("DANGER! -- requestLeaderShip -- remoteID (" + remoteID + ") smaller than MyID (" + parent.ID + ")");
				}
				else: {
					system.println("INFO -- INBOUND requestLeaderShip -- new Leader: " + remoteID);
				};
				parent.onNewLeader(remoteID);
			}
			else: {
				system.println("INFO -- requestLeaderShip: " + remoteID + " already leader, ignoring");
			};
			
		};
		def requestPeerElection(remoteLeader) {
			system.println("INFO -- Peer Election requested. Old leader:" + remoteLeader);
			parent.status := "electing";
			def snapOnlinePlayers := parent.onlinePlayers.deepCopy();
			def newLeader := peerElection(parent, snapOnlinePlayers);
			parent.leader := newLeader;		// We are now the new leader
			if: (newLeader == parent.ID) then:{	
				system.println("INFO -- I (" + parent.ID + ") am new leader, broadcasting leadershiprequests");
				snapOnlinePlayers.map({|plyr|
					plyr<-requestLeaderShip(parent.ID)@OneWay // no return value required
					});				
			}
			else:{
				system.println("INFO -- We (" + parent.ID + ") do not become leader; new Leader is: " + newLeader);
			};
			parent.onNewLeader(newLeader);
		};
		def startSession(session){
			system.println("INFO -- INBOUND -- StartSession, sessionID:" + session.ID);
			parent.status := "inSession";
			parent.currSession := session;
			parent.gui.displayLog("Starting new session: " + currSession.ID);			
			
			def duration := (session.endTime - now());
			when: duration elapsed: {
			parent.gui.displayLog("End of session: " + currSession.ID);
			system.println("INFO -- Session over");
			parent.sessionOver(session);
			};
			
		};
		def resumeSession(session){
			// DEPRECATED
		};
	};
};